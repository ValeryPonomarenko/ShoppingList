apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion versions.targetSdkVersion
    defaultConfig {
        applicationId "me.vponomarenko.shoppinglist"
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode getVersionCode(versions.versionName)
        versionName versions.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation appmodules.data
    implementation appmodules.domain
    implementation appmodules.presentation.list
    implementation appmodules.presentation.edit

    implementation deps.kotlin
    implementation deps.appcompat
    implementation deps.navfragment
    implementation deps.navui
    implementation deps.componentsmanager
    implementation deps.dagger
    kapt deps.daggercompiler
    implementation deps.rxjava
    implementation deps.rxandroid

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}


int getVersionCode(versionName) {
    def (major, minor, patch) = versionName.toLowerCase().tokenize('.')

    (major, minor, patch) = [major, minor, patch].collect{it.toInteger()}

    (major * 1000000) + (minor * 1000) + (patch * 10)
}
